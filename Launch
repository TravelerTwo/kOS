// Deploy fairing if it's (they're) staged and altitude > body:atm:height - 1000

PARAMETER apo IS 100000. // Name?
PARAMETER inc IS 0.      // Name?

CLEARSCREEN.
SET config:ipu TO 600.
SET steeringmanager:rollcontrolanglerange TO 180.
SET steeringmanager:rolltorquefactor TO 0.5.
SET terminal:height TO 25.
SET terminal:width TO 40.
IF HASNODE { REMOVE nextnode. }

FUNCTION burn_dV {
    LOCAL rad IS body:radius + apoapsis.
    LOCAL speedApo IS sqrt(body:mu * (2 / rad - 1 / obt:semimajoraxis)).
    LOCAL speedCirc IS sqrt(body:mu / rad).
    RETURN speedCirc - speedApo.
}

FUNCTION launch_azimuth {
    PARAMETER inc.

    LOCAL az_orb IS arcsin(cos(inc) / cos(ship:latitude)).
    LOCAL speedOrb IS max(sqrt(body:mu / body:position:mag), velocity:orbit:mag + 1).
    IF inc < 0 { SET az_orb TO 180 - az_orb. }

    LOCAL V_corr IS heading(az_orb, 0) * v(0, 0, speedOrb) - vxcl(up:vector, velocity:orbit).
    LOCAL vel_E IS vdot(V_corr, heading(90, 0):vector).
    LOCAL vel_N IS vdot(V_corr, north:vector).
    RETURN arctan2(vel_E, vel_N).
}

FUNCTION pitch_angle {
    LOCAL rad IS body:radius + body:atm:height * maxTwr.

    LOCAL apo_corr IS apo * (1 + p + ship:q).
    LOCAL speedCirc_corr IS sqrt(body:mu / rad).
    RETURN sqrt((apoapsis / apo_corr + groundspeed / speedCirc_corr) / 2) * 90.
}

// LIBRARY.
IF core:currentvolume:files:keys:contains("lib_info") { RUN ONCE lib_info. } ELSE { RUNONCEPATH("0:/lib_info"). }

// LISTS.
LOCAL burnInfo IS list().
LOCAL stageInfo IS list().

// PHYSICAL PROPERTIES.
LOCAL g IS body:mu / body:position:sqrmagnitude.
LOCAL p IS body:atm:altitudepressure(altitude).

// VARIABLES.
LOCAL apoDiff IS 0.
LOCAL burndV IS burn_dV().
LOCAL burnMass IS 0.
LOCAL burnTime IS 0.
LOCAL maxAccel IS ship:availablethrust / ship:mass.
LOCAL maxthrustAtStage IS ship:maxthrustat(0).
LOCAL navAlt IS 100 + alt:radar.
LOCAL navPitch IS 90.
LOCAL navYaw IS 90.
LOCAL maxTwr IS 1.
LOCAL pitchOrb IS vang(up:vector, velocity:orbit).
LOCAL stagedV IS 0.
LOCAL ve IS 0.
LOCAL V_burn IS facing:vector.

// SYSTEM CONTROL.
LOCAL modeBurn IS FALSE.
LOCAL modeCirc IS FALSE.
LOCAL modeCoast IS FALSE.
LOCAL steeringCtrl IS lookdirup(facing:vector, facing:topvector).
LOCAL throttleCtrl IS 1.

PRINT " ".
PRINT " Local gravity  =".
PRINT " Local TWR      =".
PRINT " Pressure (ATM) =".
PRINT " Pressure (Q)   =".
PRINT " Throttle       =".
PRINT " ".
PRINT " Eccentricity   =".
PRINT " Relative apo   =".
PRINT " Relative inc   =".
PRINT " Relative pitch =".
PRINT "--------------------------".
PRINT " ".
PRINT " Burn start     =".
PRINT " Burn time      =".
PRINT " Node ETA       =".
PRINT "--------------------------".
PRINT " ".
PRINT " Node angle     =".
PRINT " Node delta-v   =".
PRINT " Stage delta-v  =".
PRINT "--------------------------".

sas OFF.
gear OFF.
brakes OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO throttleCtrl.

IF apo <= body:atm:height { SET apo TO body:atm:height + 10000. }

IF abs(inc) > 180 - abs(ship:latitude) { SET inc TO 180. }
ELSE IF abs(inc) < abs(ship:latitude) { SET inc TO 0. }

IF inc = 180 { SET navYaw TO 270. }

UNTIL sas {

      IF NOT modeCirc {

            SET apoDiff TO apo - apoapsis.
            SET navPitch TO min(navPitch, 90 - min(pitch_angle(), pitchOrb)).
            SET pitchOrb TO vang(up:vector, velocity:orbit).
            SET pitchOrb TO max(pitchOrb, 90 - pitchOrb).
            SET throttleCtrl TO 2 * apoDiff / (2 * velocity:orbit:mag + maxAccel).
            SET throttleCtrl TO max(0.05, min(1, throttleCtrl)) - ship:q^2.

            IF inc <> 0 AND inc <> 180 { SET navYaw TO launch_azimuth(inc). }
            SET steeringCtrl TO CHOOSE heading(navYaw, navPitch) IF airspeed > 100 AND alt:radar > navAlt ELSE lookdirup(up:vector, facing:topvector).

            PRINT round(p,3) + "     " at(18,3).
            PRINT round(ship:q,3) + "     " at(18,4).
            PRINT round(apoDiff) + "  " at(18,8).
            PRINT round(navPitch - (90 - vang(up:vector, facing:vector)),2) + "    " at(18,10).
            PRINT round(eta:apoapsis,1) + "    " at(18,15).

            IF NOT modeCoast AND apoapsis >= apo - 1 {
                  SET burndV TO burn_dV().
                  SET burnTime TO burndV / maxAccel.
                  SET modeCoast TO TRUE.
                  LOCK throttle TO 0.

                  PRINT round(burnTime,1) + "    " at(18,14).
                  PRINT round(eta:apoapsis,1) + "    " at(18,15).
                  PRINT round(burndV,3) + "      " at(18,19).

                  IF eta:apoapsis < burnTime {
                        SET apo TO apo + 10000.
                        SET modeCoast TO FALSE.
                        LOCK throttle TO throttleCtrl.
                  }

            } ELSE IF modeCoast AND altitude > body:atm:height {
                  SET modeCirc TO TRUE.
                  SET stageInfo TO stage_info().
                  SET ve TO 9.80665 * stageInfo[0][4].

                  SET burndV TO burn_dV().
                  SET burnInfo TO burn_info(burndV, stageInfo).
                  SET burnTime TO burnInfo[0].
                  PRINT round(burnInfo[1],1) + "    " at(18,13).
                  ADD node(time:seconds + eta:apoapsis, 0, 0, burndV).
            }

      } ELSE {

            IF NOT modeBurn AND nextnode:eta <= burnInfo[1] {
                  SET V_burn TO nextnode:burnvector.
                  SET modeBurn TO TRUE.
                  LOCK throttle TO throttleCtrl.
            }

            IF nextnode:deltav:mag < stagedV {
                  SET burnMass TO ship:mass / constant:e^(nextnode:deltav:mag / ve).
                  SET burnTime TO (ship:mass - burnMass) / stageInfo[0][3].
            }

            SET stagedV TO ve * ln(ship:mass / stageInfo[0][1]).
            SET steeringCtrl TO CHOOSE lookdirup(V_burn, facing:topvector) IF throttleCtrl < 1 ELSE lookdirup(nextnode:burnvector, up:vector).
            SET throttleCtrl TO min(1, burnTime).

            PRINT round(apoapsis - max(0, periapsis)) + "  " at(18,8).
            PRINT round(burnTime,1) + "    " at(18,14).
            PRINT round(nextnode:eta,1) + "    " at(18,15).
            PRINT round(vang(nextnode:burnvector, facing:vector),1) + "    " at(18,18).
            PRINT round(nextnode:deltav:mag,2) + "    " at(18,19).
            PRINT round(stagedV,2) + "    " at(18,20).

            IF burnTime < 0.001 OR modeBurn AND vang(V_burn, nextnode:burnvector) > 90 { BREAK. }
      }

      IF maxthrustAtStage > ship:maxthrustat(0) OR maxthrustAtStage = 0 {
            WAIT 0.4.
            stage.

            WAIT UNTIL stage:ready.
            SET maxthrustAtStage TO ship:maxthrustat(0).

            IF modeCirc AND maxthrustAtStage <> 0 {
                  stageInfo:remove(0).
                  SET ve TO 9.80665 * stageInfo[0][4].

                  SET burnInfo TO burn_info(burndV, stageInfo).
                  SET burnTime TO burnInfo[0].
            }
      }

      SET g TO body:mu / body:position:sqrmagnitude.
      SET p TO body:atm:altitudepressure(altitude).
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET maxTwr TO maxAccel / g.

      PRINT round(g,2) + "    " at(18,1).
      PRINT round(maxTwr,2) + "    " at(18,2).
      PRINT round(throttle,2) + "    " at(18,5).
      PRINT round(obt:eccentricity,4) + "      " at(18,7).
      PRINT round(abs(inc) - obt:inclination,2) + "    " at(18,9).

      WAIT 0.
}

steeringmanager:resettodefault().
SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
rcs OFF.
sas ON.
