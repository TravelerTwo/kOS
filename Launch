PARAMETER desiredApo IS 100000.
PARAMETER desiredInc IS 0.

// TO-DO.
// Retrograde launch, pitch isn't perfect, but it's working.
// Very low TWR upper stages might need something to help them pitch up to compensate for lack of thrust.
// stage_dV check perhaps stages a bit early.

// Find next stage TWR, possiblethrust / currentmass might be good enough.
// Adapt desiredEta to use next stage TWR after using whatever dV remains in current stage.
// Experiment with adjusting desiredEta to make up for pitching up before burn.
// desiredEta * sin(pitchOrbit) or desiredEta - desiredEta * cos(pitchOrbit).

CLEARSCREEN.
SET config:ipu TO 500.
SET steeringmanager:rollcontrolanglerange TO 180.
SET steeringmanager:rolltorquefactor TO 0.5.
SET terminal:height TO 20.
SET terminal:width TO 40.

FUNCTION burn_dV {
    LOCAL r IS body:radius + apoapsis.
    LOCAL velApo IS sqrt(body:mu * (2 / r - 1 / obt:semimajoraxis)).
    LOCAL velCirc IS sqrt(body:mu / r).
    RETURN velCirc - velApo.
}

FUNCTION desired_pitch {
    LOCAL atmPitch IS desiredApo * (1 + p + ship:q).
    LOCAL spdPitch IS body:atm:height * maxTwr.
    LOCAL desiredSpeed IS sqrt(body:mu / (body:radius + spdPitch)).
    LOCAL desiredPitch IS sqrt(apoapsis / atmPitch) + sqrt(groundspeed / desiredSpeed).
    RETURN desiredPitch / 2 * 90.
}

FUNCTION launch_azimuth {
    PARAMETER inc.

    LOCAL V_orb IS max(sqrt(body:mu / body:position:mag), velocity:orbit:mag + 1).
    LOCAL az_orb IS arcsin(cos(inc) / cos(ship:latitude)).
    IF inc < 0 { SET az_orb TO 180 - az_orb. }

    LOCAL V_star IS heading(az_orb, 0) * v(0, 0, V_orb).
    LOCAL V_ship_h IS vxcl(up:vector, velocity:orbit).
    LOCAL V_corr IS V_star - V_ship_h.
    LOCAL vel_n IS vdot(V_corr, north:vector).
    LOCAL vel_e IS vdot(V_corr, heading(90, 0):vector).
    LOCAL az_corr IS arctan2(vel_e, vel_n).
    RETURN az_corr.
}

FUNCTION stage_dV {
    LOCAL elist_active IS list().
    LOCAL elist_all IS list().
    LOCAL rlist IS list().
    LIST ENGINES IN elist_all.

    LOCAL fuelAmount IS 0.
    LOCAL fuelDensity IS 0.
    LOCAL fuelMass IS 0.
    LOCAL i IS 0.
    LOCAL ispFlow IS 0.
    LOCAL ispThrust IS 0.
    LOCAL ispTotal IS 0.

    FOR e IN elist_all {
        IF e:ignition AND NOT e:flameout { elist_active:add(e). }
        WAIT 0.
    }

    FOR e IN elist_active {
        UNTIL i = e:consumedresources:keys:length {
            IF rlist:contains(e:consumedresources:keys[i]) { BREAK. }
            rlist:add(e:consumedresources:keys[i]).
            SET fuelAmount TO e:consumedresources[e:consumedresources:keys[i]]:amount.
            SET fuelDensity TO e:consumedresources[e:consumedresources:keys[i]]:density.
            SET fuelMass TO fuelMass + fuelAmount * fuelDensity.
            SET i TO i + 1.
            WAIT 0.
        }

        SET i TO 0.
        SET ispFlow TO ispFlow + e:maxthrustat(0) / (e:vacuumisp * 9.80665).
        SET ispThrust TO ispThrust + e:maxthrustat(0).
        SET ispTotal TO ispThrust / (ispFlow * 9.80665).
        WAIT 0.
    }

    RETURN 9.80665 * ispTotal * ln(ship:mass / (ship:mass - fuelMass)).
}

// PHYSICAL PROPERTIES.
LOCAL g IS body:mu / body:position:sqrmagnitude.
LOCAL p IS body:atm:altitudepressure(altitude).

// VARIABLES.
LOCAL burndV IS burn_dV().
LOCAL maxAccel IS ship:availablethrust / ship:mass.
LOCAL maxthrustAtStage IS ship:maxthrustat(0).
LOCAL i IS 1.
LOCAL navAlt IS 100 + alt:radar.
LOCAL navPitch IS 90.
LOCAL navYaw IS 90.
LOCAL PIDApo IS PIDLoop(0.75, 1.15, 0.005, 0, 1).
LOCAL pitchCraft IS vang(up:vector, facing:vector).
LOCAL pitchOrbit IS vang(up:vector, velocity:orbit).

LOCAL desiredEta IS burndV / max(0.0001, maxAccel) / 2.
LOCAL maxTwr IS maxAccel / g.

IF abs(desiredInc) > 180 - abs(ship:latitude) { SET desiredInc TO 180. }
ELSE IF abs(desiredInc) < abs(ship:latitude) { SET desiredInc TO 0. }

IF desiredApo <= body:atm:height { SET desiredApo TO body:atm:height + 10000. }
IF desiredInc = 180 { SET navYaw TO 270. }

// SYSTEM CONTROL.
LOCAL modeCirc IS FALSE.
LOCAL steeringCtrl IS lookdirup(facing:vector, facing:topvector).
LOCAL throttleCtrl IS 1.

PRINT " ".
PRINT " Local gravity  =".
PRINT " Local TWR      =".
PRINT " Pressure (ATM) =".
PRINT " Pressure (Q)   =".
PRINT " Throttle       =".
PRINT " ".
PRINT " Relative inc   =".
PRINT " Relative pitch =".
PRINT "--------------------------".
PRINT " ".
PRINT " Burn at ETA    =".
PRINT " Eccentricity   =".
PRINT " ".
PRINT " Relative apo   =".
PRINT " Relative ETA   =".
PRINT " Relative speed =".
PRINT "--------------------------".

sas OFF.
gear OFF.
brakes OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO throttleCtrl.

UNTIL sas {

      IF NOT modeCirc {

            SET navPitch TO 90 - min(desired_pitch(), pitchOrbit).
            SET throttleCtrl TO 1 - ship:q^2.

            IF apoapsis > desiredApo - 2500 {
                  LOCAL stagedV IS stage_dV().
                  SET burndV TO burn_dV().
                  SET modeCirc TO TRUE.
                  SET throttleCtrl TO 0.
                  IF stagedV < burndV {
                        SET i TO 30.
                        PRINT round(stagedV - burndV,2) at(18,16).
                        WAIT 1.
                  }
            }

      } ELSE {

            SET burndV TO burn_dV().
            SET desiredEta TO burndV / max(0.0001, maxAccel) / 2.
            SET navPitch TO 90 - pitchOrbit.
            SET PIDApo:setpoint TO desiredEta + i.
            SET throttleCtrl TO PIDApo:update(time:seconds, eta:apoapsis).

            PRINT round(desiredEta,2) + "    " at(18,11).
            PRINT round(obt:eccentricity,2) + "    " at(18,12).
            PRINT round(eta:apoapsis - desiredEta,2) + "    " at(18,15).
            PRINT round(burndV,2) + "    " at(18,16).

            IF burndV < 0.25 { BREAK. }
      }

      IF maxthrustAtStage > ship:maxthrustat(0) OR maxthrustAtStage = 0 {
            WAIT 1.
            stage.
            WAIT UNTIL stage:ready.
            SET i TO 1.
            SET maxthrustAtStage TO ship:maxthrustat(0).
      }

      IF desiredInc <> 0 AND desiredInc <> 180 { SET navYaw TO launch_azimuth(desiredInc). }

      IF alt:radar > navAlt { SET steeringCtrl TO heading(navYaw, navPitch). }
      ELSE { SET steeringCtrl TO lookdirup(up:vector, facing:topvector). }

      SET g TO body:mu / body:position:sqrmagnitude.
      SET p TO body:atm:altitudepressure(altitude).
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET maxTwr TO maxAccel / g.
      SET pitchCraft TO 90 - vang(up:vector, facing:vector).
      SET pitchOrbit TO vang(up:vector, velocity:orbit).
      SET pitchOrbit TO max(pitchOrbit, 90 - pitchOrbit).

      PRINT round(g,2) + "    " at(18,1).
      PRINT round(maxTwr,2) + "    " at(18,2).
      PRINT round(p,3) + "     " at(18,3).
      PRINT round(ship:q,3) + "     " at(18,4).
      PRINT round(throttleCtrl,2) + "    " at(18,5).
      PRINT round(abs(desiredInc) - obt:inclination,2) + "    " at(18,7).
      PRINT round(navPitch - pitchCraft,2) + "    " at(18,8).
      PRINT round(desiredApo - apoapsis) + "  " at(18,14).

      WAIT 0.
}

steeringmanager:resettodefault().
SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
rcs OFF.
sas ON.
