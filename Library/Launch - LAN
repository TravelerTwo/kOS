PARAMETER inc, orb_lan.

FUNCTION mAngle {
    PARAMETER a.
    UNTIL a >= 0 { SET a TO a + 360. }
    RETURN mod(a, 360).
}

LOCAL rel_lng IS 0.
LOCAL g_lan IS 0.
LOCAL node_angle IS 0.
LOCAL eta IS 0.

rcs ON.

UNTIL NOT rcs {

    CLEARSCREEN.

    SET rel_lng TO arcsin(tan(ship:latitude) / tan(inc)).
    SET g_lan TO mAngle(orb_lan + rel_lng - body:rotationangle).
    SET node_angle TO mAngle(g_lan - ship:longitude).
    SET eta TO (node_angle / 360) * body:rotationperiod.

    PRINT "Seconds until: " + round(eta).
    PRINT "Time until:    " + timestamp(eta):clock.

    SET rel_lng TO 180 - rel_lng.
    SET g_lan TO mAngle(orb_lan + rel_lng - body:rotationangle).
    SET node_angle TO mAngle(g_lan - ship:longitude).
    SET eta TO (node_angle / 360) * body:rotationperiod.

    PRINT " ".
    PRINT "Seconds until: " + round(eta).
    PRINT "Time until:    " + timestamp(eta):clock.

    WAIT 0.
}
