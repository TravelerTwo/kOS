// See if it's possible to reduce IPU use in fork?
// Add safety check for input?
// Remove interval?

PARAMETER dest IS target.

CLEARSCREEN.
SET config:ipu TO 2000.
SET terminal:height TO 25.
SET terminal:width TO 50.

FUNCTION new_param {
    LOCAL chr IS terminal:input:getchar().

    IF chr:matchespattern("[a-z0-9]") {
        SET input TO input + chr.
        PRINT input at(15,n).

    } ELSE IF chr = terminal:input:backspace {
        SET input TO "".
        PRINT "            " at(15,n).

    } ELSE IF chr = terminal:input:enter {
        IF input <> "" {
            IF i < 2 { SET input TO input:toscalar * m. }
            rsvp_settings:add(options[i], input).
            PRINT input at(15,i+n+2).
        }
        PRINT "x" at(29,i+n+2).
        PRINT "          " at(15,n).
        SET i TO i + 1.
        SET input TO "".
    }
}

// LISTS.
LOCAL options IS list("search_duration", "search_interval").
LOCAL rsvp_settings IS lex("verbose", true, "create_maneuver_nodes", "first").

// SYSTEM CONTROL.
LOCAL done IS FALSE.
LOCAL i IS 0.
LOCAL input IS "".
LOCAL m IS 21600.
LOCAL n IS 15.
LOCAL origin IS body.
LOCAL s IS " (days)".
IF body = dest:body { SET origin TO ship. }

LOCAL syn_period IS abs(origin:obt:period * dest:obt:period / (origin:obt:period - dest:obt:period)).

IF dest:istype("body") {
    rsvp_settings:add("final_orbit_periapsis", dest:atm:height + 100000).
    options:add("final_orbit_type").
    options:add("final_orbit_orientation").
    SET n TO n + 2.
}

IF syn_period / m < 2 {
    SET m TO 3600.
    SET s TO " (hours)".
}

PRINT " ".
PRINT " Target      : " + dest:name:substring(0,1):toupper + dest:name:substring(1,dest:name:length-1).
PRINT " ".
PRINT " ORBITAL PERIODS".
PRINT " Calendar    : " + time:calendar.
PRINT " Origin      : " + round(origin:obt:period / m).
PRINT " Target      : " + round(dest:obt:period / m).
PRINT " Synodic     : " + round(syn_period / m) + s.
PRINT " ".
PRINT " DEFAULT VALUES".
PRINT " Duration    : " + round(max(syn_period, max(origin:obt:period, dest:obt:period)) / m).
PRINT " Interval    : " + round(min(origin:obt:period, dest:obt:period) / 2 / m).
IF dest:istype("body") {
    PRINT " Orbit type  : none, " + "circular":toupper + ", elliptical".
    PRINT " Orientation : " + "prograde":toupper + ", polar, retrograde".
}

PRINT "--------------------------".
PRINT " ".
PRINT " Leave blank to use default, enter to confirm".
PRINT "       Input :".
PRINT " ".
PRINT " Duration    :             [   ]".
PRINT " Interval    :             [   ]".
IF dest:istype("body") {
    PRINT " Orbit type  :             [   ]".
    PRINT " Orientation :             [   ]".
}

PRINT "--------------------------".

UNTIL done {

      IF terminal:input:haschar { new_param(). }

      IF i >= options:length { SET done TO TRUE. }

      IF abort { BREAK. }

      IF mod(time:second,2) = 0 { PRINT "_" at(15 + input:length,n). }
      ELSE { PRINT " " at(15 + input:length,n). }

      WAIT 0.
}

abort OFF.

IF done {
    PRINT "Running search...".
    runoncepath("0:/rsvp/main").
    rsvp:goto(dest, rsvp_settings).
}
