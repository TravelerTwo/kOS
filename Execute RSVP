PARAMETER dest.
PARAMETER desiredDeparture IS 0.
PARAMETER desiredOrbit IS "circular".

CLEARSCREEN.

FUNCTION new_param {
    LOCAL char IS terminal:input:getchar().

    IF char:matchespattern("[0-9]") {
        SET input TO input + char.
        PRINT input at(i,5).

    } ELSE IF char = terminal:input:backspace {
        SET i TO 10.
        SET input TO "".
        PRINT "          " at(i,5).

    } ELSE IF char = terminal:input:enter {
        IF input <> "" { SET desiredDuration TO input:toscalar * 21600. }
        SET done TO TRUE.
    }
}

LOCAL done IS FALSE.
LOCAL i IS 10.
LOCAL input IS "".
LOCAL desiredDuration IS 0.
LOCAL syn_period IS abs(body:obt:period * dest:obt:period / (body:obt:period - dest:obt:period)).

SET desiredDeparture TO time:seconds + max(120, desiredDeparture * 21600).

PRINT " ".
PRINT " Days: " + time:day.
PRINT " " + body:name + ": " + time(body:obt:period):day + " (days)".
PRINT " " + dest:name:substring(0,1):toupper + dest:name:substring(1,dest:name:length-1) + ": " + time(dest:obt:period):day.
PRINT " Synodic: " + time(syn_period):day.
PRINT " New max: _".
PRINT " Leave blank to use default".
PRINT " ".

UNTIL done {

      IF terminal:input:haschar { new_param(). }

      IF abort { BREAK. }

      IF mod(time:second,2) = 0 { PRINT "_" at(i + input:length,5). }
      ELSE { PRINT " " at(i + input:length,5). }

      WAIT 0.
}

IF desiredDuration = 0 { SET desiredDuration TO max(body:obt:period, max(dest:obt:period, syn_period)). }

IF done {
    PRINT "Running search...".
    runoncepath("0:/rsvp/main").
    LOCAL options is lexicon(
        "verbose", true,
        "create_maneuver_nodes", "first",
        "earliest_departure", desiredDeparture,
        "search_duration", desiredDuration,
        "final_orbit_periapsis", max(100000, dest:atm:height + 100000),
        "final_orbit_type", desiredOrbit
    ).
    rsvp:goto(dest, options).
}

abort OFF.
