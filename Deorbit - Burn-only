PARAMETER deployChutes IS FALSE.

CLEARSCREEN.
SET config:ipu TO 400.
SET terminal:height TO 20.
SET terminal:width TO 40.

FUNCTION h_accel {
    LOCAL v IS vxcl(up:vector, velocity:surface).
    LOCAL hAccel IS -v / 5.
    RETURN hAccel:normalized * min(hAccel:mag, burnAccel).
}

// PHYSICAL PROPERTIES.
LOCAL g IS body:mu / body:position:sqrmagnitude.

// VARIABLES.
LOCAL b IS ship:bounds.
LOCAL maxAccel IS ship:availablethrust / ship:mass.

LOCAL burnAccel IS verticalspeed^2 / 2 / (b:bottomaltradar - groundspeed) + g.
LOCAL burnThrot IS burnAccel / maxAccel.
LOCAL navTime IS groundspeed / maxAccel.

// SYSTEM CONTROL.
LOCAL modeBurn IS FALSE.
LOCAL steeringCtrl IS facing.
LOCAL throttleCtrl IS 0.

PRINT " ".
PRINT " Local gravity =".
PRINT " Local TWR     =".
PRINT " ".
PRINT " Radar alt     =".
PRINT "--------------------------".
PRINT " ".
PRINT " Burn accel    =".
PRINT " Burn throt    =".
PRINT " ".
PRINT " Groundspeed   =".
PRINT " Verticalspeed =".
PRINT " ".
PRINT " Time to stop  =".
PRINT "--------------------------".

sas OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO 0.

IF body:atm:exists {
    brakes ON. 
    rcs ON.
}

UNTIL ship:status = "landed" OR ship:status = "splashed" OR sas {

      IF verticalspeed > -1 { SET burnAccel TO g. }
      ELSE { SET burnAccel TO verticalspeed^2 / 2 / b:bottomaltradar + g. }

      SET g TO body:mu / body:position:sqrmagnitude.
      SET burnThrot TO burnAccel / maxAccel.
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET navTime TO (verticalspeed + sqrt(max(0, 2 * b:bottomaltradar * max(g, burnAccel * throttle) + verticalspeed^2))) / burnAccel.
      SET steeringCtrl TO lookdirup(up:vector * burnAccel + h_accel(), facing:topvector).
      SET throttleCtrl TO burnThrot.

      IF NOT modeBurn AND verticalspeed < -1 AND (burnThrot > 0.9 OR b:bottomaltradar < 250) {
            SET modeBurn TO TRUE.
            LOCK throttle TO throttleCtrl.

      } ELSE IF modeBurn AND (verticalspeed > 0 OR burnThrot < 0.5 AND b:bottomaltradar > 250) {
            SET modeBurn TO FALSE.
            LOCK throttle TO 0.
      }

      IF body:atm:exists AND deployChutes AND NOT chutessafe { chutessafe ON. }

      IF NOT gear AND verticalspeed < -1 AND b:bottomaltradar < 250 { gear ON. }

      PRINT round(g,2) + "    " at(18,1).
      PRINT round(maxAccel / g,2) + "    " at(18,2).
      PRINT round(b:bottomaltradar) + "  " at(18,4).
      PRINT round(burnAccel,2) + "    " at(18,7).
      PRINT round(burnThrot,2) + "    " at(18,8).
      PRINT round(groundspeed,2) + "    " at(18,10).
      PRINT round(verticalspeed,2) + "    " at(18,11).
      PRINT round(navTime,2) + "    " at(18,13).

      WAIT 0.
}

SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
rcs OFF.
sas ON.
