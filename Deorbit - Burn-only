PARAMETER deployChutes IS FALSE.

CLEARSCREEN.
SET config:ipu TO 300.
SET terminal:height TO 22.
SET terminal:width TO 40.

// PHYSICAL PROPERTIES.
LOCAL g IS body:mu / body:position:sqrmagnitude.

// VARIABLES.
LOCAL b IS ship:bounds.
LOCAL maxAccel IS ship:availablethrust / ship:mass.

LOCAL burnAccel IS verticalspeed^2 / 2 / b:bottomaltradar + g.
LOCAL burnThrot IS burnAccel / maxAccel.

// SYSTEM CONTROL.
LOCAL modeBurn IS FALSE.
LOCAL steeringCtrl IS facing:vector.
LOCAL throttleCtrl IS 0.

PRINT " ".
PRINT " Burn accel    =".
PRINT " Burn throt    =".
PRINT " Groundspeed   =".
PRINT " Local gravity =".
PRINT " Local TWR     =".
PRINT " Radar alt     =".
PRINT "--------------------------".

sas OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO 0.

IF body:atm:exists {
    brakes ON. 
    rcs ON.
}

UNTIL ship:status = "landed" OR ship:status = "splashed" OR sas {

      IF NOT modeBurn AND verticalspeed < -1 AND (burnThrot > 0.9 OR b:bottomaltradar < 250) {
            SET modeBurn TO TRUE.
            LOCK throttle TO throttleCtrl.

      } ELSE IF modeBurn AND (verticalspeed > 0 OR burnThrot < 0.5 AND b:bottomaltradar > 250) {
            SET modeBurn TO FALSE.
            LOCK throttle TO 0.
      }

      IF body:atm:exists AND deployChutes AND NOT chutessafe { chutessafe ON. }

      IF NOT gear AND verticalspeed < -1 AND b:bottomaltradar < 250 { gear ON. }

      SET g TO body:mu / body:position:sqrmagnitude.
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET burnAccel TO verticalspeed^2 / 2 / (b:bottomaltradar - groundspeed) + g.
      SET burnThrot TO burnAccel / maxAccel.
      SET steeringCtrl TO lookdirup(up:vector * 5 + -velocity:surface, facing:topvector).

      IF verticalspeed < -1 { SET throttleCtrl TO burnThrot. }
      ELSE { SET throttleCtrl TO g / maxAccel. }

      PRINT round(burnAccel,2) + "    " at(18,1).
      PRINT round(burnThrot,2) + "    " at(18,2).
      PRINT round(groundspeed,2) + "    " at(18,3).
      PRINT round(g,2) + "    " at(18,4).
      PRINT round(maxAccel / g,2) + "    " at(18,5).
      PRINT round(b:bottomaltradar) + "  " at(18,6).

      WAIT 0.
}

SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
rcs OFF.
sas ON.
