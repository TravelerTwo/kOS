CLEARSCREEN.
SET config:ipu TO 2000.
SET terminal:height TO 30.
SET terminal:width TO 40.

FUNCTION sqrt_neg {
    PARAMETER p.
    RETURN CHOOSE -sqrt(-p) IF p < 0 ELSE sqrt(p).
}

// PHYSICAL PROPERTIES.
LOCAL b IS ship:bounds.
LOCAL g IS body:mu / body:position:sqrmagnitude.

// VARIABLES.
LOCAL maxAccel IS ship:availablethrust / ship:mass.
LOCAL navPitch IS 45.
LOCAL srfPitch IS vang(up:vector, -velocity:surface).

LOCAL hAccel IS facing:vector.
LOCAL vAccel IS facing:vector.
LOCAL burnAccel IS verticalspeed^2 / 2 / (b:bottomaltradar - 1) + g.
LOCAL burnThrot IS burnAccel / maxAccel.
LOCAL navTime IS -verticalspeed / maxAccel.

// SYSTEM CONTROL.
LOCAL modeBurn IS FALSE.
LOCAL steeringCtrl IS facing:vector.
LOCAL throttleCtrl IS 0.

PRINT " ".
PRINT " Local gravity =".
PRINT " Local TWR     =".
PRINT " ".
PRINT " Radar alt     =".
PRINT " Throttle      =".
PRINT " ".
PRINT " Time to stop  =".
PRINT "--------------------------".
PRINT " ".
PRINT " h accel       =".
PRINT " v accel       =".
PRINT " ".
PRINT " Groundspeed   =".
PRINT " Verticalspeed =".
PRINT "--------------------------".

sas OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO 0.

IF body:atm:exists {
    brakes ON. 
    rcs ON.
}

UNTIL ship:status = "landed" OR ship:status = "splashed" OR sas {

      SET g TO body:mu / body:position:sqrmagnitude.
      SET hAccel TO 2 * vxcl(up:vector, -velocity:surface) / max(5, navTime - 5).
      SET hAccel:mag TO min(hAccel:mag, sqrt_neg((burnAccel / cos(navPitch))^2 - burnAccel^2)).
      SET vAccel TO burnAccel * up:vector.
      SET burnAccel TO CHOOSE g IF verticalspeed > -1 ELSE verticalspeed^2 / 2 / (b:bottomaltradar - max(groundspeed, 1)) + g.
      SET burnThrot TO burnAccel / maxAccel / cos(navPitch).
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET navPitch TO min(navPitch, srfPitch).
      SET navTime TO (sqrt_neg(verticalspeed^2 + 2 * g * b:bottomaltradar) + verticalspeed) / g.
      SET srfPitch TO vang(up:vector, -velocity:surface).
      SET steeringCtrl TO CHOOSE hAccel + vAccel IF modeBurn ELSE -velocity:surface.
      SET steeringCtrl TO lookdirup(steeringCtrl, facing:topvector).
      SET throttleCtrl TO burnThrot.

      IF NOT gear AND modeBurn AND navTime < 5 { gear ON. }

      IF NOT modeBurn AND verticalspeed < -1 AND (burnThrot > 0.9 OR b:bottomaltradar < 500) {
            SET modeBurn TO TRUE.
            LOCK throttle TO throttleCtrl.


      } ELSE IF modeBurn AND (verticalspeed > 0 OR burnThrot < 0.5 AND b:bottomaltradar > 500) {
            SET modeBurn TO FALSE.
            LOCK throttle TO 0.
      }

      PRINT round(g,2) + "    " at(18,1).
      PRINT round(maxAccel / g,2) + "    " at(18,2).
      PRINT round(b:bottomaltradar) + "  " at(18,4).
      PRINT round(throttleCtrl,2) + "    " at(18,5).
      PRINT round(navTime,2) + "    " at(18,7).
      PRINT round(hAccel:mag,2) + "    " at(18,10).
      PRINT round(burnAccel,2) + "    " at(18,11).
      PRINT round(groundspeed,2) + "    " at(18,13).
      PRINT round(verticalspeed,2) + "    " at(18,14).

      WAIT 0.
}

SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
rcs OFF.
sas ON.
