CLEARSCREEN.
SET config:ipu TO 300.
SET terminal:height TO 20.
SET terminal:width TO 40.

FUNCTION burn_time {
    LOCAL burnFlow IS ship:availablethrust / (ispTotal * 9.80665).
    LOCAL burnMass IS ship:mass / constant():e^(nextnode:deltav:mag / (ispTotal * 9.80665)).
    RETURN (ship:mass - burnMass) / burnFlow.
}

// LISTS.
LIST ENGINES IN elist.

// VARIABLES.
LOCAL burnTime IS 0.
LOCAL burnVec IS nextnode:burnvector.
LOCAL ispFlow IS 0.
LOCAL ispThrust is 0.
LOCAL ispTotal IS 0.
LOCAL maxAccel IS ship:availablethrust / ship:mass.
LOCAL nodeTime IS 0.
LOCAL nodeVang IS vang(burnVec, nextnode:burnvector).

FOR e IN elist {
    IF e:ignition {
        IF e:flameout { e:shutdown(). }
        ELSE {
            SET ispFlow TO ispFlow + e:availablethrust / (e:isp * 9.80665).
            SET ispThrust TO ispThrust + e:availablethrust.
        }
    }
    WAIT 0.
}

SET ispTotal TO ispThrust / (ispFlow * 9.80665).

// SYSTEM CONTROL.
LOCAL modeBurn IS FALSE.
LOCAL steeringCtrl IS lookdirup(nextnode:burnvector, facing:topvector).
LOCAL throttleCtrl IS 0.

PRINT " ".
PRINT " ISP           = " + round(ispTotal,1).
PRINT " Burn time     =".
PRINT " Half time     =".
PRINT " Node ETA      =".
PRINT "--------------------------".
PRINT " ".
PRINT " Error steer   =".
PRINT " Node angle    =".
PRINT " Node delta-V  =".
PRINT "--------------------------".

rcs OFF.
sas OFF.
LOCK steering TO steeringCtrl.
LOCK throttle TO 0.

UNTIL sas {

      IF NOT modeBurn AND nextnode:eta <= nodeTime {
            SET modeBurn TO TRUE.
            SET burnVec TO nextnode:burnvector.
            LOCK throttle TO throttleCtrl.
      }

      SET burnTime TO burn_time().
      SET maxAccel TO ship:availablethrust / ship:mass.
      SET nodeTime TO burnTime * 0.5.
      SET nodeVang TO vang(burnVec, nextnode:burnvector).
      SET throttleCtrl TO min(1, burnTime).

      IF throttleCtrl < 1 { SET steeringCtrl TO lookdirup(burnVec, facing:topvector).
      } ELSE { SET steeringCtrl TO lookdirup(nextnode:burnvector, facing:topvector). }

      PRINT round(burnTime,1) + "    "  at(17,2).
      PRINT round(nodeTime,1) + "    " at(17,3).
      PRINT round(nextnode:eta,1) + "    " at(17,4).
      PRINT round(vang(nextnode:burnvector, facing:vector),1) + "    " at(17,7).
      PRINT round(nodeVang,2) + "      " at(17,8).
      PRINT round(nextnode:deltav:mag,3) + "      " at(17,9).

      IF nextnode:deltav:mag < 0.001 OR nodeVang > 90 { BREAK. }

      WAIT 0.
}

SET ship:control:pilotmainthrottle TO 0.
SET throttleCtrl TO 0.
UNLOCK ALL.
sas ON.
